apply plugin: 'com.android.application'

// 全局函数
def buildTime = new Date().format("YYYYMMdd")
def gitShortHash  = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def getAppName(){
    return "APP"
}
def genFileName(versionCode, versionName, versionNameSuffix){
    return getAppName() + "_" + versionCode + "_" + versionName + versionNameSuffix + ".apk"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.lixplor.gradleconfigdemo"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // 签名配置
    signingConfigs {
        release {

        }
    }

    // 构建类型
    buildTypes {
        // 开发包
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "_dev_b" + buildTime + "@" + gitShortHash
            resValue "string", "app_name", getAppName() + "_dev"
            buildConfigField("String", "SERVER_BASE_URL", "\"http://api.host.com/dev\"")
            buildConfigField("boolean", "LOG_ENABLED", "true")
            buildConfigField("boolean", "BUGTAGS_ENABLED", "false")
            signingConfig signingConfigs.debug
            shrinkResources false
            zipAlignEnabled true
        }
        // 测试包
        fortest {
            applicationIdSuffix ".test" // todo 测试环境需要使用测试包名注册单独的三方平台应用
            versionNameSuffix "_test_b" + buildTime + "@" + gitShortHash
            resValue "string", "app_name", getAppName() + "_test"
            buildConfigField("String", "SERVER_BASE_URL", "\"http://api.host.com/test\"")
            buildConfigField("boolean", "LOG_ENABLED", "false")
            buildConfigField("boolean", "BUGTAGS_ENABLED", "true")
            signingConfig signingConfigs.debug
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // 正式包
        release {
            resValue "string", "app_name", getAppName()
            buildConfigField("String", "SERVER_BASE_URL", "\"http://api.host.com/release\"")
            buildConfigField("boolean", "LOG_ENABLED", "false")
            buildConfigField("boolean", "BUGTAGS_ENABLED", "true")
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 输出文件名
    android.applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    output.outputFile = new File(output.outputFile.parent, genFileName(defaultConfig.versionCode, defaultConfig.versionName, variant.buildType.versionNameSuffix));
            }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    testCompile 'junit:junit:4.12'
}
